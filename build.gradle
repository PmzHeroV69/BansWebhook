plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'net.kyori.blossom' version '1.3.0'
}

group = 'ir.pmzhero'
version = '2.0'
java.sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'
    compileOnly 'org.spigotmc:spigot-api:1.13-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-api:1.16-R0.4'
    compileOnly 'com.gitlab.ruany:LiteBansAPI:0.3.4'
    compileOnly files('lib/AdvancedBan.jar')
    implementation 'club.minnced:discord-webhooks:0.8.0'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

shadowJar {
    classifier ''
}

blossom {
    replaceToken '$(version)', version
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(Arrays.asList("plugin.yml", "bungee.yml")) {
        expand props
    }
}
